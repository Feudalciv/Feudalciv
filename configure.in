dnl configure.in for freeciv
dnl Process this file with autoconf to produce a configure script.

dnl Initialize with some random file to ensure the source is here.
AC_INIT(common/game.c)
AM_CONFIG_HEADER(config.h)

PACKAGE=freeciv

dnl client/server should always have the same major and minor versions
dnl different patch versions are compatible
MAJOR_VERSION=1
MINOR_VERSION=7
PATCH_VERSION=1

VERSION=${MAJOR_VERSION}.${MINOR_VERSION}.${PATCH_VERSION}

dnl Similar to following are already done by AM_INIT_AUTOMAKE:
dnl (but different in terms of producing quoted vs bare strings)
dnl  AC_DEFINE_UNQUOTED(PACKAGE, $PACKAGE)
dnl  AC_DEFINE_UNQUOTED(VERSION, $VERSION)
dnl  AC_SUBST(VERSION)

AC_DEFINE_UNQUOTED(MAJOR_VERSION, $MAJOR_VERSION)
AC_DEFINE_UNQUOTED(MINOR_VERSION, $MINOR_VERSION)
AC_DEFINE_UNQUOTED(PATCH_VERSION, $PATCH_VERSION)
AC_DEFINE_UNQUOTED(VERSION_STRING, "${MAJOR_VERSION}.${MINOR_VERSION}.${PATCH_VERSION}")

dnl Initialize automake stuff
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

AM_MAINTAINER_MODE

dnl Evaluate options. Example:
AC_ARG_ENABLE(debug,
 	[  --enable-debug=[no/minimum/yes] 
 	                  turn on debugging [default=minimum]],,
	enable_debug=minimum
)

if test "x$enable_debug" = "xyes"; then
  test "$cflags_set" = set || CFLAGS="$CFLAGS -g"
  CPPFLAGS="$CPPFLAGS -DDEBUG"
else
  if test "x$enable_debug" = "xno"; then
    CPPFLAGS="$CPPFLAGS -DNDEBUG"
  fi
fi

AC_ARG_WITH(xaw3d,
 	[  --with-xaw3d            compile with Xaw3d instead of Xaw ],
	WITH_XAW3D=1
)

AC_ARG_ENABLE(client,
[  --disable-client        do not compile the client],
[case "${enableval}" in
  yes) client=true ;;
  no)  client=false ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for --disable-client) ;;
esac], [client=true])
AM_CONDITIONAL(CLIENT, test x$client = xtrue)

AC_ARG_ENABLE(server,
[  --disable-server        do not compile the server],
[case "${enableval}" in
  yes) server=true ;;
  no)  server=false ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for --disable-server) ;;
esac], [server=true])
AM_CONDITIONAL(SERVER, test x$server = xtrue)


dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_PROG_LN_S
AC_PROG_RANLIB

AC_ARG_PROGRAM

dnl Programs already checked by AM_INIT_AUTOMAKE:
dnl  AC_PROG_MAKE_SET

if test x$client = xtrue; then
    dnl Checks for X:
    AC_PATH_XTRA
    dnl CFLAGS="$CFLAGS $X_CFLAGS"
    X_LIBS="$X_LIBS $X_PRE_LIBS"

    dnl Checks for X libs:
    FC_CHECK_X_LIB(X11, XOpenDisplay, , AC_MSG_ERROR("Need X11"))
    FC_CHECK_X_LIB(Xext, XShapeCombineMask)
    FC_CHECK_X_LIB(Xt, main)
    FC_CHECK_X_LIB(Xmu, main)
    FC_CHECK_X_LIB(Xpm, XpmReadFileToPixmap, , AC_MSG_ERROR("Need Xpm library"))
    if test x$WITH_XAW3D = x; then
	FC_CHECK_X_LIB(Xaw, main)
    else
	FC_CHECK_X_LIB(Xaw3d, main)
    fi
    dnl Don't do following because don't want server to link with X:
    dnl LIBS="$LIBS $X_LIBS $X_EXTRA_LIBS"
fi

dnl Checks for non-X libraries:
AC_CHECK_LIB(socket,main)
AC_CHECK_LIB(nsl,main)
AC_CHECK_LIB(nls,main)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/ioctl.h sys/time.h unistd.h)
if test x$client = xtrue; then
    dnl Want to get appropriate -I flags:
    SAVE_CPPFLAGS="$CPPFLAGS"
    CPPFLAGS="$CPPFLAGS $X_CFLAGS"
    AC_CHECK_HEADERS(X11/xpm.h)
    CPPFLAGS="$SAVE_CPPFLAGS"
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gethostname select socket strerror strstr)

dnl autoscan also recommends gettimeofday in AC_CHECK_FUNCS, but in the code
dnl that is already protected by ifdef CHRONO, and is for selective
dnl developer use only -- dwp

dnl export where the datadir is going to be installed
AC_EXPAND_DIR(FREECIV_DATADIR, "$datadir/freeciv")
AC_DEFINE_UNQUOTED(FREECIV_DATADIR, "$FREECIV_DATADIR")

AC_OUTPUT(Makefile data/Makefile common/Makefile ai/Makefile client/Makefile server/Makefile)
