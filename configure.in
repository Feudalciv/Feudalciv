dnl configure.in for freeciv
dnl Process this file with autoconf to produce a configure script.

dnl Initialize with some random file to ensure the source is here.
AC_INIT(common/game.c)
AM_CONFIG_HEADER(config.h)

dnl May not be quite correct, but autoconf version 2.9 is reported 
dnl not to work, and version 2.12 is known to work:
AC_PREREQ(2.12)

PACKAGE=freeciv

dnl client/server should always have the same major and minor versions
dnl different patch versions are compatible
MAJOR_VERSION=1
MINOR_VERSION=7
PATCH_VERSION=2

VERSION=${MAJOR_VERSION}.${MINOR_VERSION}.${PATCH_VERSION}

dnl Similar to following are already done by AM_INIT_AUTOMAKE:
dnl (but different in terms of producing quoted vs bare strings)
dnl  AC_DEFINE_UNQUOTED(PACKAGE, $PACKAGE)
dnl  AC_DEFINE_UNQUOTED(VERSION, $VERSION)
dnl  AC_SUBST(VERSION)

AC_DEFINE_UNQUOTED(MAJOR_VERSION, $MAJOR_VERSION)
AC_DEFINE_UNQUOTED(MINOR_VERSION, $MINOR_VERSION)
AC_DEFINE_UNQUOTED(PATCH_VERSION, $PATCH_VERSION)
AC_DEFINE_UNQUOTED(VERSION_STRING, "${MAJOR_VERSION}.${MINOR_VERSION}.${PATCH_VERSION}")

dnl Initialize automake stuff
AM_INIT_AUTOMAKE($PACKAGE, $VERSION)

AM_MAINTAINER_MODE

dnl Evaluate options. Example:
AC_ARG_ENABLE(debug,
 	[  --enable-debug=[no/minimum/yes] 
 	                  turn on debugging [default=minimum]],,
	enable_debug=minimum
)

if test "x$enable_debug" = "xyes"; then
  test "$cflags_set" = set || CFLAGS="$CFLAGS -g"
  CPPFLAGS="$CPPFLAGS -DDEBUG"
else
  if test "x$enable_debug" = "xno"; then
    CPPFLAGS="$CPPFLAGS -DNDEBUG"
  fi
fi

AC_ARG_WITH(xaw3d,
 	[  --with-xaw3d            compile with Xaw3d instead of Xaw ],
	WITH_XAW3D=1
)

AC_ARG_ENABLE(client,
[  --disable-client        do not compile the client],
[case "${enableval}" in
  yes) client=true ;;
  no)  client=false ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for --disable-client) ;;
esac], [client=true])
AM_CONDITIONAL(CLIENT, test x$client = xtrue)

AC_ARG_ENABLE(server,
[  --disable-server        do not compile the server],
[case "${enableval}" in
  yes) server=true ;;
  no)  server=false ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for --disable-server) ;;
esac], [server=true])
AM_CONDITIONAL(SERVER, test x$server = xtrue)

AC_ARG_ENABLE(make_data,
[  --disable-make-data     do not recurse make into data directories
                          ok to disable unless you will make install],
[case "${enableval}" in
  yes) make_data=true ;;
  no)  make_data=false ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for --disable-make-data) ;;
esac], [make_data=true])
AM_CONDITIONAL(MAKE_DATA, test x$make_data = xtrue)

AC_ARG_ENABLE(cvs_deps,
 	[  --disable-cvs-deps      remove cvs-source deps calcs, which require gmake,gcc],,
	enable_cvs_deps="maybe"
)
CVS_DEPS=$enable_cvs_deps
AC_SUBST(CVS_DEPS)

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_PROG_LN_S
AC_PROG_RANLIB

AC_ARG_PROGRAM

dnl Programs already checked by AM_INIT_AUTOMAKE:
dnl  AC_PROG_MAKE_SET

if test -n "$GCC"; then
   CFLAGS="$CFLAGS -Wall"
fi

if test "$CVS_DEPS" = "maybe"; then
   dnl Should also check for gmake?
   if test -n "$GCC"; then
      CVS_DEPS="yes"
   else
      CVS_DEPS="no"
   fi
fi

if test x$client = xtrue; then
    dnl Checks for X:
    AC_PATH_XTRA

    dnl Try to get additional Xpm paths:
    FC_XPM_PATHS

    if test "$xpm_incdir" != "no"; then
        X_CFLAGS="$X_CFLAGS -I$xpm_incdir"
    fi
    if test "$xpm_libdir" != "no"; then
        X_LIBS="$X_LIBS -L$xpm_libdir"
	dnl Try using R values set in AC_PATH_XTRA:
        if test "$ac_R_nospace" = "yes"; then
            X_LIBS="$X_LIBS -R$xpm_libdir"
        elif test "$ac_R_space" = "yes"; then
            X_LIBS="$X_LIBS -R $xpm_libdir"
        fi
    fi
    fc_save_X_LIBS="$X_LIBS"
    X_LIBS="$X_LIBS $X_PRE_LIBS"

    dnl Checks for X libs:
    FC_CHECK_X_LIB(X11, XOpenDisplay, , 
        AC_MSG_ERROR(Need X11; perhaps try/adjust --x-libraries))
    FC_CHECK_X_LIB(Xext, XShapeCombineMask)

    dnl Insert X_PRE_LIBS (eg -lSM -lICE) into X_EXTRA_LIBS here:
    X_EXTRA_LIBS="$X_PRE_LIBS $X_EXTRA_LIBS"
    X_LIBS="$fc_save_X_LIBS"

    FC_CHECK_X_LIB(Xt, main)
    FC_CHECK_X_LIB(Xmu, main)
    FC_CHECK_X_LIB(Xpm, XpmReadFileToPixmap, , 
        AC_MSG_ERROR(Need Xpm library; perhaps try/adjust --with-xpm-lib))
    if test x$WITH_XAW3D = x; then
	FC_CHECK_X_LIB(Xaw, main)
    else
	FC_CHECK_X_LIB(Xaw3d, main)
    fi
    dnl Don't do following because don't want server to link with X:
    dnl LIBS="$LIBS $X_LIBS $X_EXTRA_LIBS"
fi

dnl Checks for non-X libraries:
if test x$server = xtrue; then
    dnl For the client, these libs (if required) should already be present
    dnl in X_EXTRA_LIBS, due to AC_PATH_XTRA, and we don't want to include 
    dnl them multiple times on the link line.
    dnl The use of AC_CHECK_FUNC is to avoid wrong libs on IRIX.
    AC_CHECK_FUNC(gethostbyname)
    if test $ac_cv_func_gethostbyname = no; then
        AC_CHECK_LIB(nsl, gethostbyname, SERVER_LIBS="-lnsl $SERVER_LIBS")
    fi
    AC_CHECK_FUNC(connect)
    if test $ac_cv_func_connect = no; then
        AC_CHECK_LIB(socket, connect, SERVER_LIBS="-lsocket $SERVER_LIBS")
    fi
fi
AC_SUBST(SERVER_LIBS)

AC_CHECK_LIB(nls,main)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/ioctl.h sys/time.h unistd.h)
if test x$client = xtrue; then
    dnl Want to get appropriate -I flags:
    fc_save_CPPFLAGS="$CPPFLAGS"
    CPPFLAGS="$CPPFLAGS $X_CFLAGS"
    AC_CHECK_HEADER(X11/xpm.h, , 
        AC_MSG_ERROR(Need X11/xpm.h header; perhaps try/adjust --with-xpm-include))
    CPPFLAGS="$fc_save_CPPFLAGS"
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gethostname select strerror strstr)

dnl autoscan also recommends gettimeofday in AC_CHECK_FUNCS, but in the code
dnl that is already protected by ifdef CHRONO, and is for selective
dnl developer use only -- dwp

dnl We would AC_CHECK_FUNCS for socket as well, except it is complicated
dnl by the fact that the -lsocket is in X_EXTRA_LIBS and/or SERVER_LIBS,
dnl and not in LIBS.

dnl export where the datadir is going to be installed
FC_EXPAND_DIR(FREECIV_DATADIR, "$datadir/freeciv")
AC_DEFINE_UNQUOTED(FREECIV_DATADIR, "$FREECIV_DATADIR")

AC_OUTPUT(Makefile data/Makefile data/default/Makefile data/civ1/Makefile data/classic/Makefile common/Makefile ai/Makefile client/Makefile server/Makefile undep.sh, [ chmod +x undep.sh ; ./undep.sh ])
