dnl configure.in for freeciv
dnl Process this file with autoconf to produce a configure script.

dnl Initialize with some random file to ensure the source is here.
AC_INIT(common/game.c)
AM_CONFIG_HEADER(config.h)

PACKAGE=freeciv

dnl client/server should always have the same major and minor versions
dnl different patch versions are compatible
MAJOR_VERSION=1
MINOR_VERSION=7
PATCH_VERSION=1

VERSION=${MAJOR_VERSION}.${MINOR_VERSION}.${PATCH_VERSION}

AC_DEFINE_UNQUOTED(PACKAGE, $PACKAGE)
AC_DEFINE_UNQUOTED(VERSION, $VERSION)
AC_DEFINE_UNQUOTED(MAJOR_VERSION, $MAJOR_VERSION)
AC_DEFINE_UNQUOTED(MINOR_VERSION, $MINOR_VERSION)
AC_DEFINE_UNQUOTED(PATCH_VERSION, $PATCH_VERSION)
AC_DEFINE_UNQUOTED(VERSION_STRING, "${MAJOR_VERSION}.${MINOR_VERSION}.${PATCH_VERSION}")
AC_SUBST(VERSION)

dnl Initialize automake stuff
AM_INIT_AUTOMAKE($PACKAGE, $VERSION, no-define)

AM_MAINTAINER_MODE

dnl Evaluate options. Example:
AC_ARG_ENABLE(debug,
 	[  --enable-debug=[no/minimum/yes] 
 	                  turn on debugging [default=minimum]],,
	enable_debug=minimum
)

if test "x$enable_debug" = "xyes"; then
  test "$cflags_set" = set || CFLAGS="$CFLAGS -g"
  CPPFLAGS="$CPPFLAGS -DDEBUG"
else
  if test "x$enable_debug" = "xno"; then
    CPPFLAGS="$CPPFLAGS -DNDEBUG"
  fi
fi

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_CPP
AC_PROG_LN_S
AC_PROG_RANLIB

AC_ARG_PROGRAM

dnl Programs already checked by AM_INIT_AUTOMAKE:
dnl  AC_PROG_MAKE_SET

dnl Checks for libraries.
AC_PATH_XTRA
dnl CFLAGS="$CFLAGS $X_CFLAGS"
X_LIBS="$X_LIBS $X_PRE_LIBS"

FC_CHECK_X_LIB(X11, XOpenDisplay, , AC_MSG_ERROR("Need X11"))
FC_CHECK_X_LIB(Xext, XShapeCombineMask)
FC_CHECK_X_LIB(Xt, main)
FC_CHECK_X_LIB(Xmu, main)
FC_CHECK_X_LIB(Xpm, XpmReadFileToPixmap, , AC_MSG_ERROR("Need Xpm library"))
FC_CHECK_X_LIB(Xaw, main)
dnl LIBS="$LIBS $X_LIBS $X_EXTRA_LIBS"

AC_CHECK_LIB(socket,main)
AC_CHECK_LIB(nsl,main)
AC_CHECK_LIB(nls,main)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/ioctl.h sys/time.h unistd.h X11/xpm.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(gethostname select socket strerror strstr)

dnl autoscan also recommends gettimeofday in AC_CHECK_FUNCS, but in the code
dnl that is already protected by ifdef CHRONO, and is for selective
dnl developer use only -- dwp

dnl export where the datadir is going to be installed
CPPFLAGS="$CPPFLAGS -DFREECIV_DATADIR=\\\"\$(pkgdatadir)\\\""

AC_OUTPUT(Makefile data/Makefile common/Makefile ai/Makefile client/Makefile server/Makefile)
